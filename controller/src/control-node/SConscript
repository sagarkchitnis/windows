# -*- mode: python; -*-
#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('BuildEnv')
import sys

def MapBuildDir(list):
    return map(lambda x: '#/' + Dir('.').path + '/../' + x, list)

env = BuildEnv.Clone()

libcontrol_node = env.Library('control_node', ['control_node.cc'])

env.Append(CPPPATH = [env['TOP'] + '/base/sandesh'])
env.Append(CPPPATH = [env['TOP'] + '/db'])
env.Append(CPPPATH = [env['TOP'] + '/ifmap'])
env.Append(CPPPATH = [env['TOP'] + '/bgp'])
env.Append(CPPPATH = [env['TOP'] + '/tools/sandesh/library/common'])
ControlNodeSandeshGenFiles = env.SandeshGenCpp('sandesh/control_node.sandesh')
ControlNodeSandeshGenSrcs = env.ExtractCpp(ControlNodeSandeshGenFiles)

setup_sources = [
           'setup.py',
           'MANIFEST.in',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(env.Install(Dir("."), "#controller/src/control-node/sandesh/" + file))

# Generate the sandesh UVE source files
control_node_pkg  = env.SandeshGenPy('sandesh/control_node.sandesh', 'control_node/', False)
cpuinfo_pkg = env.SandeshGenPy('#controller/src/base/sandesh/cpuinfo.sandesh', 'control_node/control_node/', False)
process_info_pkg = env.SandeshGenPy('#controller/src/base/sandesh/process_info.sandesh', 'control_node/control_node/', False)
ifmap_pkg = env.SandeshGenPy('#controller/src/ifmap/ifmap_server_show.sandesh', 'control_node/control_node/', False)
env.Depends(ifmap_pkg, control_node_pkg)
env.Depends(cpuinfo_pkg, control_node_pkg)
env.Depends(process_info_pkg, control_node_pkg)

rel_path = Dir('control_node').path
def BuildInfoAction(target, source, env):
    env.GenerateBuildInfoPyCode(path=rel_path)
    
build_info_rules = [env.Command(target='buildinfo.py', source = None, action=BuildInfoAction)]

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(control_node_pkg)
sdist_depends.extend(ifmap_pkg)
sdist_depends.extend(cpuinfo_pkg)
sdist_depends.extend(process_info_pkg)
sdist_depends.extend(build_info_rules)
sdist_gen = env.Command('dist', File('setup.py'), cd_cmd + 'python setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)
env.Alias("control-node:node_mgr", sdist_gen)

control_node_sandesh_files_ = []
for src in ControlNodeSandeshGenSrcs:
    objname = src.replace('.cpp', '.o')
    obj = env.Object(objname, src)
    control_node_sandesh_files_.append(obj)

env.Prepend(LIBS=['bgp',
                  'bgp_ifmap_config',
                  'bgp_schema',
                  'bgp_xmpp',
                  'extended_community',
                  'xmpp_unicast',
                  'xmpp_multicast',
                  'xmpp_enet',
                  'control_node',
                  'peer_sandesh',
                  'table_sandesh',
                  'origin_vn',
                  'routing_instance',
                  'routing_policy',
                  'rtarget',
                  'security_group',
                  'tunnel_encap',
                  'ifmap_vnc',
                  'ifmap_server',
                  'ifmap_common',
                  'route',
                  'net',
                  'ds',
                  'ifmapio',
                  'xmpp',
                  'xml',
                  'sandeshvns',
                  'sandesh',
                  'httpc',
                  'http',
                  'http_parser',
                  'curl',
                  'db',
                  'process_info',
                  'io',
                  'base',
                  'cpuinfo',
                  'process_info',
                  'nodeinfo',
                  'pugixml',
                  'libxml2',
                  'boost_regex',
		  'boost_chrono',
                  'boost_program_options',
                  'boost_filesystem',
                  'libeay32', 'ssleay32'])

if sys.platform != 'darwin':
    env.Append(LIBS=['rt'])

libs = MapBuildDir(['base',
                    'bgp',
                    'bgp/inet',
                    'bgp/inet6',
                    'bgp/inet6vpn',
                    'bgp/ermvpn',
                    'bgp/evpn',
                    'bgp/extended-community',
                    'bgp/l3vpn',
                    'bgp/origin-vn',
                    'bgp/routing-instance',
                    'bgp/routing-policy',
                    'bgp/rtarget',
                    'bgp/security_group',
                    'bgp/tunnel_encap',
                    'control-node',
                    'db',
                    'ifmap',
                    'io', 
                    'net',
                    'route',
                    'xmpp',
                    'xml',
                    'discovery/client',
                    'http/client'])

env.Append(LIBPATH=libs)
env.Append(LIBPATH = env['TOP'] + '/schema')
env.Append(CPPPATH = env['TOP'])
env.Append(CPPPATH = env['TOP'] + '/control-node')
env.Append(CPPPATH = env['TOP'] + '/discovery/client')
env.Append(CPPPATH = env['TOP'] + '/io')

lib_l3vpn = File('../bgp/l3vpn/libbgp_l3vpn.lib')
lib_inet = File('../bgp/inet/libbgp_inet.lib')
lib_inet6 = File('../bgp/inet6/libbgp_inet6.lib')
lib_inet6vpn = File('../bgp/inet6vpn/libbgp_inet6vpn.lib')
lib_ermvpn = File('../bgp/ermvpn/libbgp_ermvpn.lib')
lib_evpn = File('../bgp/evpn/libbgp_evpn.lib')
lib_rtarget = File('../bgp/rtarget/librtarget.lib')
lib_ifmap_server = File('../ifmap/libifmap_server.lib')
lib_sandesh = File('../sandesh/library/cpp/sandesh.lib')
lib_cpuinfo = File('../base/libcpuinfo.lib')
lib_process_info = File('../base/libprocess_info.lib')
lib_nodeinfo = File('../base/libnodeinfo.lib')
lib_base = File('../base/libbase.lib')
lib_extend_community = File('../bgp/extended-community/libextended_community.lib')



buildinfo_dep_libs = ['../bgp/libbgp.lib', 
                      '../bgp/libbgp_ifmap_config.lib',
                      '../schema/libbgp_schema.lib', '../schema/libxmpp_unicast.lib', 
                      '../schema/libxmpp_multicast.lib', '../schema/libxmpp_enet.lib',
                      '../control-node/libcontrol_node.lib', '../bgp/routing-instance/librouting_instance.lib', 
                      '../bgp/routing-policy/librouting_policy.lib',
                      '../bgp/origin-vn/liborigin_vn.lib',
                      '../bgp/extended-community/libextended_community.lib',
                      '../bgp/rtarget/librtarget.lib', '../bgp/security_group/libsecurity_group.lib', '../schema/libifmap_vnc.lib', 
                      '../ifmap/libifmap_server.lib', '../ifmap/libifmap_common.lib', '../route/libroute.lib', '../net/libnet.lib', 
                      '../../lib/libifmapio.lib', '../xmpp/libxmpp.lib', '../xml/libxml.lib', '../../lib/sandeshvns.lib', 
                      '../../lib/sandesh.lib', '../../lib/libhttp.lib', '../../lib/libhttp_parser.lib',
                      '../db/libdb.lib', '../io/libio.lib', '../base/libbase.lib', '../base/libcpuinfo.lib', '../base/libprocess_info.lib',
                      '../base/libnodeinfo.lib', '../../lib/libpugixml.lib',
		      ]

env.GenerateBuildInfoCode(
	target = ['buildinfo.h', 'buildinfo.cc'],
	source = ControlNodeSandeshGenSrcs + buildinfo_dep_libs +
		['main.cc', 'options.cc', 'sandesh/control_node_sandesh.cc'],
	path = Dir('.').path)

ctrlnode = env.Program(target = 'contrail-control',
                       source = ['buildinfo.cc',
                                 'factory.cc',
                                 'main.cc',
                                 'options.cc',
                                 'sandesh/control_node_sandesh.cc'] +
                                 control_node_sandesh_files_)

env.Default(ctrlnode)

env.Depends(ctrlnode, lib_l3vpn)
env.Depends(ctrlnode, lib_inet)
env.Depends(ctrlnode, lib_inet6)
env.Depends(ctrlnode, lib_inet6vpn)
env.Depends(ctrlnode, lib_ermvpn)
env.Depends(ctrlnode, lib_evpn)
env.Depends(ctrlnode, lib_rtarget)
env.Depends(ctrlnode, lib_ifmap_server)

env.Alias('control-node', ctrlnode)
env.Alias('contrail-control', ctrlnode)

env.SConscript('test/SConscript', exports='BuildEnv', duplicate = 0)

#
# contrail-control message documentation
#
doc_files = []
doc_files += env.SandeshGenDoc('sandesh/control_node.sandesh')
doc_files += env['BGP_DOC_FILES']
doc_files += env['BASE_DOC_FILES']
doc_files += env['IO_DOC_FILES']
doc_files += env['DB_DOC_FILES']
doc_files += env['XMPP_DOC_FILES']
doc_files += env['IFMAP_SERVER_DOC_FILES']
doc_files += env['SANDESH_DOC_FILES']
doc_files += env['DISCOVERY_CLIENT_DOC_FILES']

# Please update sandesh/common/vns.sandesh on process name change
env.Alias('install', env.Install(env['INSTALL_BIN'], ctrlnode))
env.Alias('install', env.Install(env['INSTALL_CONF'], 
    'contrail-control.conf'))
env.Alias('install', env.Install(env['INSTALL_CONF'], 
    'supervisord_control.conf'))
env.Alias('install', env.Install(env['INSTALL_CONF'] + 
    '/supervisord_control_files', 'contrail-control.ini'))
env.Alias('install', env.Install(env['INSTALL_CONF'] + 
    '/supervisord_control_files', 'contrail-control.rules'))
env.Alias('install', env.InstallAs(env['INSTALL_INITD'] + 
    '/contrail-control', 'contrail-control.initd.supervisord'))
env.Alias('install', env.Install(env['INSTALL_MESSAGE_DOC'] +
    '/contrail-control/', doc_files))
